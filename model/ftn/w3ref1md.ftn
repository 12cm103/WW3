!/ ------------------------------------------------------------------- /
      MODULE W3REF1MD
!/
!/                  +-----------------------------------+
!/                  | WAVEWATCH III                     |
!/                  |            F. Ardhuin             |
!/                  |                        FORTRAN 90 |
!/                  | Last update :         31-May-2011 |
!/                  +-----------------------------------+
!/
!/    31-Mar-2010 : Origination.                        ( version 3.14.IFREMER )
!/    03-Sep-2010 : Clean up                            ( version 3.14.IFREMER )
!/    31-May-2011 : Adding variable reflections         ( version 4.01 )
!/ 
!  1. Purpose :
!
!     This module computes : 
!        - shoreline reflection 
!        - unresolved islands and iceberg reflections
!
!  2. Variables and types :
!
!      Name      Type  Scope    Description
!     ----------------------------------------------------------------
!     ----------------------------------------------------------------
!
!  3. Subroutines and functions :
!
!      Name      Type  Scope    Description
!     ----------------------------------------------------------------
!      W3SREF    Subr. Public   Reflection of waves (shorline, islands...)
!     ----------------------------------------------------------------
!
!  4. Subroutines and functions used :
!
!      Name      Type  Module   Description
!     ----------------------------------------------------------------
!      STRACE    Subr. W3SERVMD Subroutine tracing.
!     ----------------------------------------------------------------
!
!  5. Remarks :
!
!
!  6. Switches :
!
!     !/S  Enable subroutine tracing.
!
!  7. Source code :
!/
!/ ------------------------------------------------------------------- /
!/
!
      PUBLIC
!/
!/ Public variables
!/
!
!/
      CONTAINS
!/ ------------------------------------------------------------------- /
      SUBROUTINE W3SREF(A, CG, WN, EMEAN, FMEAN, DEPTH, CX1, CY1, REFLC, REFLD,     &
                         DELX, DELY, DELA, TRNX, TRNY, BERG, DT,  S)
!/
!/                  +-----------------------------------+
!/                  | WAVEWATCH III           NOAA/NCEP |
!/                  |            F. Ardhuin             |
!/                  |                        FORTRAN 90 |
!/                  | Last update :         31-May-2011 |
!/                  +-----------------------------------+
!/
!/    06-May-2010 : Origination.                          ( version 3.14-Ifremer )
!/    31-May-2011 : Introduction of amplitude-dependent R (version 4.xx)
!/
!  1. Purpose :
!
!     Computes coastal and iceberg/island reflections
!
!  2. Method :
!
!     Adds the reflected components from 2 types of sources: 
!        shoreline reflection, subgrid obstruction and icebergs 
!      
!
!  3. Parameters :
!
!     Parameter list
!     ----------------------------------------------------------------
!  A         R.A.  I   Action density spectrum (1-D)
!       CG        R.A.  I   Group velocities.
!       WN        R.A.  I   Wavenumbers.
!       DEPTH     Real  I   Mean water depth.
!       S         R.A.  O   Source term (1-D version).
!       D         R.A.  O   Diagonal term of derivative (1-D version).
!     ----------------------------------------------------------------
!
!  4. Subroutines used :
!
!      Name      Type  Module   Description
!     ----------------------------------------------------------------
!      STRACE    Subr. W3SERVMD Subroutine tracing.
!     ----------------------------------------------------------------
!
!  5. Called by :
!
!      Name      Type  Module   Description
!     ----------------------------------------------------------------
!      W3SRCE    Subr. W3SRCEMD Source term integration.
!      W3EXPO    Subr.   N/A    Point output post-processor.
!      GXEXPO    Subr.   N/A    GrADS point output post-processor.
!     ----------------------------------------------------------------
!
!  6. Error messages :
!
!       None.
!
!  7. Remarks :
!
!  8. Structure :
!
!     See source code.
!
!  9. Switches :
!
!     !/S  Enable subroutine tracing.
!
! 10. Source code :
!
!/ ------------------------------------------------------------------- /
      USE CONSTANTS
      USE W3GDATMD, ONLY: NK, NTH, NSPEC, SIG, DTH, DDEN, RREF, &
                          REFPARS, ECOS, ESIN, EC2, MAPTH, MAPWN, &
        SIG2, DSII
!/S      USE W3SERVMD, ONLY: STRACE
!/
!
      IMPLICIT NONE
!/
!/ ------------------------------------------------------------------- /
!/ Parameter list
!/
      REAL, INTENT(IN)        :: CG(NK), WN(NK), DEPTH, EMEAN, FMEAN
      REAL, INTENT(IN)        :: A(NTH,NK)
      REAL, INTENT(IN)        :: CX1, CY1, DT
      INTEGER, INTENT(IN)     :: REFLD(4)
      REAL, INTENT(IN)        :: REFLC(4), DELX, DELY, DELA, TRNX, &
                                 TRNY, BERG
      REAL, INTENT(OUT)       :: S(NSPEC)
!/
!/ ------------------------------------------------------------------- /
!/ Local parameters
!/ 
      INTEGER         :: ISPEC, IK, ITH, ITH2, ITH3
!/S      INTEGER, SAVE           :: IENT = 0
      LOGICAL, SAVE   :: FIRST = .TRUE.
       
      REAL            :: R0, R1, R2, R3, R4, R2X, R2Y
      REAL            :: FAC1, FAC2, FAC3, FAC4, RAMP0, RAMP, MICHEFAC
!/
!/ ------------------------------------------------------------------- /
!/
!/S      CALL STRACE (IENT, 'W3SREF')
!
! 0.  Initializations ------------------------------------------------ *
!
!
! 1.  Sets scattering term to zero
!
      S = 0.
!
! 2.  Shoreline reflection =============================================== *
!
      IF (REFLC(1).GT.0) THEN 
      FAC1=1/(0.5*REAL(NTH))
      FAC2=1.57/(0.5*REAL(NTH))
      FAC3=2.6/(0.5*REAL(NTH))
      FAC4=1.
      R0=ABS(ECOS(REFLD(1)))
      IF (REFPARS(6).GT.0) THEN 
!
! This is the Miche parameter for a beach slope of REFPARS(7)  (default is 0.25)
!
        MICHEFAC=0.0001*GRAV**2*(REFPARS(7)**5)/(EMEAN*FMEAN**4)
        RAMP0=MIN(0.35*(ALOG10(MICHEFAC)+5)+0.2*MICHEFAC,1.2)  ! IF REFLC(1)=1, 0.30 should be 0.001
      ELSE 
        RAMP0=1.
        ENDIF
      DO IK=1, NK
!
! Includes linear frequency dependence (see Elgar et al. JPO 1994)
!       
        IF (REFPARS(6).GT.0) THEN
          RAMP=MIN(1.0,TPI*FMEAN/SIG(IK))*RAMP0
        ELSE
          RAMP=RAMP0
          END IF
        DO ITH=1, NTH
!
!   ITH is the incident wave direction
!
          R1=ECOS(1+MOD(ABS(ITH-REFLD(1)),NTH))
          R2=REFLC(1)*RAMP*A(ITH,IK)
          IF (R1.GT.0.AND.R2.GT.0) THEN 
            DO ITH2=1,NTH
!
!   ITH2 is the reflected wave direction
!
              ISPEC=ITH2+(IK-1)*NTH
              R3=ECOS(1+MOD(ABS(ITH2-REFLD(1)),NTH))
              IF (R3.LT.0) THEN 
!
! Determines direction of specular reflection: th3=pi+2*n-th1  
!
                ITH3=1+MOD(NTH/2+NTH+2*REFLD(1)-ITH-1,NTH)
                R4=ECOS(1+MOD(ABS(ITH2-ITH3),NTH))
                IF (R4.GT.0.) THEN 
!
! Tests the type of shoreline geometry
! NB: REFLD(3) or REFLD(4) is equal to 1 if the sea point is between two land points 
                  SELECT CASE (REFLD(2))
                    CASE (0)
                   ! Sharp corner: broad reflection
                      IF (REFLD(3).EQ.0)      & 
                        S(ISPEC)=S(ISPEC)+    &
                        R2*CG(IK)*ABS(ECOS(ITH2)/DELX)*FAC1
                      IF (REFLD(4).EQ.0)      & 
                        S(ISPEC)=S(ISPEC)+ &
                        R2*CG(IK)*ABS(ESIN(ITH2)/DELY)*FAC1
                    CASE (1)
                   ! mild corner: average reflection
                      IF (REFLD(3).EQ.0)      & 
                        S(ISPEC)=S(ISPEC)+    &
                        R2*CG(IK)*ABS(ECOS(ITH2)/DELX)*ABS(R4)*FAC2
                      IF (REFLD(4).EQ.0)      & 
                        S(ISPEC)=S(ISPEC)+    &
                        R2*CG(IK)*ABS(ESIN(ITH2)/DELY)*ABS(R4)*FAC2
                    CASE (2)
                   ! straight coast: narrow reflection
!
! Specular for tests 
!               
!
!  According to the shoreline direction, changes the 
!  numerical schemes (due to neighbors) in order to 
!  account for along-coast advection
!         
                      IF (REFLD(3).EQ.0)      & 
!
! Specular for tests 
!                   S(ISPEC)=S(ISPEC)+R2*CG(IK)*ABS(ECOS(ITH2)/DELX)
                        S(ISPEC)=S(ISPEC)+R2*CG(IK)*ABS(ECOS(ITH2)/DELX) &
                                              *(R4**4) *FAC3
!
! Specular for tests 
!                   S(ISPEC)=S(ISPEC)+R2*CG(IK)*ABS(ESIN(ITH2)/DELY)
                      IF (REFLD(4).EQ.0)      & 
                        S(ISPEC)=S(ISPEC)+R2*CG(IK)*ABS(ESIN(ITH2)/DELY) &
                                              *(R4**4) *FAC3
               END SELECT
              END IF  
                END IF
              END DO
              END IF
            END DO   
          END DO
        END IF
!
!  Add diffuse reflection due to subgrid islands and icebergs
!
      IF (    ((REFPARS(2).GT.0.).AND.((TRNX+TRNY).LT.2))     &
        .OR.((REFPARS(4).GT.0.).AND.(BERG.GT.0)       )   ) THEN 
        R2X=  RAMP*REFPARS(2)*MAX(0.,MIN(1.,(1-TRNX)))  &
            + RAMP*REFPARS(4)*MAX(0.,MIN(1.,(1-EXP(-BERG*DELX*0.0001))))
        R2Y=  RAMP*REFPARS(2)*MAX(0.,MIN(1.,(1-TRNY)))  &
            + RAMP*REFPARS(4)*MAX(0.,MIN(1.,(1-EXP(-BERG*DELY*0.0001))))
      FAC1=1/(0.5*REAL(NTH))
        DO IK=1, NK
          DO ITH=1, NTH
            IF (A(ITH,IK).GT.0.) THEN 
              R0=ABS(ECOS(ITH))
             R2=A(ITH,IK)

              DO ITH2=1,NTH
                ISPEC=ITH2+(IK-1)*NTH
                R3=ECOS(1+MOD(NTH+ITH2-ITH,NTH))
                IF (R3.LT.0) THEN 
                     S(ISPEC)=S(ISPEC)+ &
                               CG(IK)*R2X*R2*ABS(ECOS(ITH2)/DELX)*FAC1 &
                             + CG(IK)*R2Y*R2*ABS(ESIN(ITH2)/DELY)*FAC1
                  END IF
                END DO
              END IF
            END DO
          END DO
        END IF
!/
!/ End of W3SREF ----------------------------------------------------- /
!/
      END SUBROUTINE W3SREF!/ ------------------------------------------------------------------- /

!/
!/ End of module W3REF1MD -------------------------------------------- /
!/
      END MODULE W3REF1MD


